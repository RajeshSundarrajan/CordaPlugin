import org.jetbrains.kotlin.gradle.tasks.KotlinCompile
import static org.gradle.api.JavaVersion.VERSION_11

plugins {
    id 'org.jetbrains.kotlin.jvm' apply false
    id 'base'
}

ext.pluginsDir = "$buildDir/plugins"

subprojects {
    group = 'net.corda.cli'
    version = '0.0.1-beta'

    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            languageVersion = '1.4'
            apiVersion = '1.4'
            verbose = true
            jvmTarget = VERSION_11
            freeCompilerArgs += [
                "-Xjvm-default=all",
                "-java-parameters"
            ]
        }
    }

    tasks.withType(GenerateModuleMetadata).configureEach {
        enabled = false
    }

    tasks.register('compileAll') { task ->
        description = "Compiles all the Kotlin and Java classes, including all of the test classes."
        group = "verification"
        task.dependsOn tasks.withType(AbstractCompile)
    }
}

wrapper {
    gradleVersion = '7.3.2'
    distributionType = Wrapper.DistributionType.BIN
    distributionUrl="https://gradleproxy:gradleproxy@software.r3.com/artifactory/gradle-proxy/gradle-$gradleVersion-bin.zip"
}
